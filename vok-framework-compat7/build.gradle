plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'kotlin'

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.1"
        languageVersion = "1.1"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.1"
        languageVersion = "1.1"
    }
}

dependencies {
    compile(project(':vok-framework'))
    compile(project(':vok-framework-jpa'))
    compile "com.vaadin:vaadin-compatibility-server:$vaadin_version"

    // have to wait until jpa container is vaadin 8 compatible
    // https://github.com/vaadin/framework/issues/8717
    compile 'com.vaadin.addon:jpacontainer:4.0.0'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'vok-framework-compat7'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Vaadin-On-Kotlin, a simple way to build webapp with Kotlin and Vaadin')
                root.appendNode('name', 'Vaadin-On-Kotlin')
                root.appendNode('url', 'https://github.com/mvysny/vaadin-on-kotlin')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    pkg {
        repo = 'vaadin-on-kotlin'
        name = 'vaadin-on-kotlin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/vaadin-on-kotlin'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}
