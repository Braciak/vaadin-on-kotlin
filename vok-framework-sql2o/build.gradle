plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'kotlin'

dependencies {
    compile(project(':vok-framework'))

    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    // jackson - mark the Entity.meta property as ignored so that it won't get serialized into JSON
    compileOnly 'com.fasterxml.jackson.core:jackson-annotations:2.8.6'
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.8.6'

    // db
    compile 'org.sql2o:sql2o:1.5.4'
    compile 'com.zaxxer:HikariCP:2.7.1'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'com.h2database:h2:1.4.196'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'vok-framework-sql2o'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Vaadin-On-Kotlin, a simple way to build webapp with Kotlin and Vaadin')
                root.appendNode('name', 'Vaadin-On-Kotlin')
                root.appendNode('url', 'https://github.com/mvysny/vaadin-on-kotlin')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'vaadin-on-kotlin'
        name = 'vaadin-on-kotlin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/vaadin-on-kotlin'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}

