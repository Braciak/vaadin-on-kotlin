buildscript {
    ext.kotlin_version = '1.2.20'
    ext.vaadin8_version = '8.2.1'
    ext.karibudsl_version = '0.2.19'
    ext.vaadin10_version = '10.0.0.alpha12'
    ext.local = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.20"
    id "org.akhikhl.gretty" version "2.0.0"
    id "com.jfrog.bintray" version "1.7.3"
}

repositories {
    mavenCentral()
}

defaultTasks 'clean', 'build'

allprojects {
    group 'com.github.vok'
    version '0.3.2-SNAPSHOT'
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.vaadin.com/vaadin-addons' }
        maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
        maven { url "https://dl.bintray.com/mvysny/vaadin-on-kotlin" }
        maven { url "https://repo.vaadin.com/nexus/content/repositories/flow" }
    }

    apply plugin: 'kotlin'
    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    // for Travis to record failures: http://stackoverflow.com/questions/28614865/how-to-read-test-result-reports-on-travis-ci/28617954#28617954
    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }
}
