buildscript {
    ext.kotlin_version = '1.2.21'
    ext.vaadin8_version = '8.3.0'
    ext.karibudsl_version = '0.3.3'
    ext.kaributesting_version = '0.4.0'
    ext.vaadin10_version = '10.0.0.alpha21'
    ext.hikaricp_version = '2.7.8'
    ext.flyway_version = '5.0.7'
    ext.dynatest_version = "0.6"

    ext.local = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.3'
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.21"
    id "org.akhikhl.gretty" version "2.0.0"
    id "com.jfrog.bintray" version "1.7.3"
}

repositories {
    mavenCentral()
}

defaultTasks 'clean', 'build'

allprojects {
    group 'com.github.vok'
    version '0.4.1-SNAPSHOT'

    // Heroku
    task stage(dependsOn: ['build'])
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.vaadin.com/vaadin-addons' }
        maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
        maven { url "https://repo.vaadin.com/nexus/content/repositories/flow" }
        maven { url 'https://dl.bintray.com/mvysny/github' }
    }

    apply plugin: 'kotlin'
    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    // for Travis to record failures: http://stackoverflow.com/questions/28614865/how-to-read-test-result-reports-on-travis-ci/28617954#28617954
    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }
}
