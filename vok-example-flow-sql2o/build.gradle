plugins {
    id "com.github.ksoichiro.web.resource" version "1.7.3"
}

apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: "org.akhikhl.gretty"

def frontendWorkingDirectory = "src/main/webapp/frontend"

clean.doFirst {
    ['bower_components', 'build', 'node', 'node_modules', 'etc'].each {
        delete frontendWorkingDirectory + '/' + it
    }
}

processResources.dependsOn webResourceCompile

webResource {
    base {
        src "src/main/webapp"
        dest frontendWorkingDirectory
    }

    lib {
        dest "bower_components"
    }

    bower {
        dependencies {
            install name: "polymer", version: "~2.0.2"
            install name: "paper-toast", version: "~2.0.0"
            install name: "paper-dialog", version: "~2.0.0"
            install name: "vaadin-core", version: "2.0.0-alpha3"
            copyAll true
        }
    }
}

gretty {
    contextPath = ''
}

dependencies {
    compile(project(":vok-framework-v10-sql2o"))
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // logging
    // currently we are logging through the SLF4J API to LogBack. See src/main/resources/logback.xml file for the logger configuration
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.25'
    // this will allow us to configure Vaadin to log to SLF4J
    compile 'org.slf4j:jul-to-slf4j:1.7.25'

    // db
    compile 'org.flywaydb:flyway-core:4.2.0'
    compile 'com.h2database:h2:1.4.196'

    // REST
    compile 'org.jboss.resteasy:resteasy-servlet-initializer:3.1.3.Final'
    compile 'org.jboss.resteasy:resteasy-jackson2-provider:3.1.3.Final'
}
