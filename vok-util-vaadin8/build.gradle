plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'kotlin'

dependencies {
    compile(project(':vok-framework'))

    // Vaadin
    compile "com.github.vok.karibudsl:karibu-dsl-v8:$karibudsl_version"
    compile "com.vaadin:vaadin-server:$vaadin8_version"
    compile 'javax.servlet:javax.servlet-api:3.1.0'

    // IDEA language injections
    compile 'com.intellij:annotations:12.0'

    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'vok-util-vaadin8'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'VOK: Basic utility classes for Vaadin 8')
                root.appendNode('name', 'Vaadin-On-Kotlin')
                root.appendNode('url', 'https://github.com/mvysny/vaadin-on-kotlin')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'vaadin-on-kotlin'
        name = 'vaadin-on-kotlin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/vaadin-on-kotlin'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}

