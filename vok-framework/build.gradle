plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.6

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile 'io.michaelrocks:bimap:1.0.2'

    // logging
    testCompile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'org.slf4j:slf4j-api:1.7.21'

    // Vaadin
    compile "com.vaadin:vaadin-server:$vaadin_version"
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    testCompile 'javax.validation:validation-api:1.0.0.GA'

    // db
    compile('org.hibernate:hibernate-core:5.2.6.Final') {
        exclude group: 'javax.enterprise'  // there is no need for CDI-API nor @Inject stuff
    }
    compile 'com.vaadin.addon:jpacontainer:3.2.0'
    testCompile 'com.h2database:h2:1.4.193'

    // IDEA language injections
    compile 'com.intellij:annotations:12.0'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'vok-framework'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Vaadin-On-Kotlin, a simple way to build webapp with Kotlin and Vaadin')
                root.appendNode('name', 'Vaadin-On-Kotlin')
                root.appendNode('url', 'https://github.com/mvysny/vaadin-on-kotlin')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    pkg {
        repo = 'vaadin-on-kotlin'
        name = 'vaadin-on-kotlin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/vaadin-on-kotlin'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}
